use std::str::FromStr;
use ast::{Statement, Command, Definition, Expr, Scalar, Point, Line, Route, SIdent, PIdent, LIdent, RIdent, Ident};

grammar;

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        },
    }
};

pub Statement: Statement = {
    <Definition> ";" => Statement::Definition(<>),
    <Command> ";" => Statement::Command(<>),
    => Statement::None,
};

Command: Command = {
    "\routes" "{" <Comma<RIdent>> "}" <String> => Command::Routes(<>),
};

Definition: Definition = {
    <SIdent> "=" <SExpr> => Definition::Scalar(<>),
    <PIdent> "=" <PExpr> => Definition::Point(<>),
    <LIdent> "=" <LExpr> => Definition::Line(<>),
    <RIdent> "=" <RExpr> => Definition::Route(<>),
    <SIdent> "[" <Comma<Ident>> "]" "=" <SExpr> => Definition::ScalarMacro(<>),
    <PIdent> "[" <Comma<Ident>> "]" "=" <PExpr> => Definition::PointMacro(<>),
    <LIdent> "[" <Comma<Ident>> "]" "=" <LExpr> => Definition::LineMacro(<>),
};

Expr: Expr = {
    SExpr => Expr::Scalar(<>),
    PExpr => Expr::Point(<>),
    LExpr => Expr::Line(<>),
};

SExpr: Scalar = {
    <l:SExpr> "<"  <r:SArith> => l.lt(r),
    <l:SExpr> "<=" <r:SArith> => l.le(r),
    <l:SExpr> ">"  <r:SArith> => r.lt(l),
    <l:SExpr> ">=" <r:SArith> => r.le(l),
    <l:SExpr> "==" <r:SArith> => l.eq(r),
    SArith,
};

SArith: Scalar = {
    <l:SArith> "+" <r:SFactor> => l.add(r),
    <l:SArith> "-" <r:SFactor> => l.sub(r),
    SFactor,
};

SFactor: Scalar = {
    <l:SFactor> "*" <r:STerm> => l.mul(r),
    <l:SFactor> "/" <r:STerm> => l.div(r),
    STerm,
};

STerm: Scalar = {
    "-" <STerm> => <>.neg(),
    Number => Scalar::Num(<>),
    "(" <SExpr> ")",
    SIdent => Scalar::Ident(<>),
    <SIdent> "[" <Comma<Expr>> "]" => Scalar::Macro(<>),
};

PExpr: Point = {
    <l:PExpr> "+" <r:PFactor> => l.add(r),
    <l:PExpr> "-" <r:PFactor> => l.add(r),
    PFactor,
};

PFactor: Point = {
    <s:SFactor> "*" <p:PTerm> => p.mul(s),
    <p:PFactor> "*" <s:STerm> => p.mul(s),
    <p:PFactor> "/" <s:STerm> => p.div(s),
    <LTerm> "&" <LTerm> => Point::intersection(<>),
    PTerm,
};

PTerm: Point = {
    "(" <SExpr> "," <SExpr> ")" => Point::Pair(<>),
    "-" <PTerm> => <>.neg(),
    "(" <PExpr> ")",
    PIdent => Point::Ident(<>),
    <PIdent> "[" <Comma<Expr>> "]" => Point::Macro(<>),
};

LExpr: Line = {
    <l:LExpr> "+" <r:PFactor> => l.add(r),
    <l:LExpr> "-" <r:PFactor> => l.add(r),
    <l:LExpr> "|" <p:PFactor> => l.parallel(p),
    <l:LExpr> ":" <p:PFactor> => l.perpendicular(p),
    <l:LExpr> "'" <s:SFactor> => l.offset(s),
    LFactor,
};

LFactor: Line = {
    LTerm,
};

LTerm: Line = {
    <PTerm> "--" <PTerm> => Line::between(<>),
    <PTerm> "->" <PTerm> => Line::vector(<>),
    "(" <LExpr> ")",
    <LIdent> => Line::Ident(<>),
    <LIdent> "[" <Comma<Expr>> "]" => Line::Macro(<>),
};

RExpr: Route = {
    <r:RExpr> "'" <o:SExpr?> "," <p:PExpr> => r.extend(o, p),
    <l:RExpr> "," <r:RTerm> => l.concat(r),
    RTerm,
};

RTerm: Route = {
    <PExpr> "'" <SExpr?> "," <PExpr> => Route::start(<>),
};

pub Ident: Ident = {
    SIdent => Ident::Scalar(<>),
    PIdent => Ident::Point(<>),
    LIdent => Ident::Line(<>),
};

SIdent: SIdent = r"\$\w+" => SIdent::from_str(<>).unwrap();
PIdent: PIdent = r"@\w+"  => PIdent::from_str(<>).unwrap();
LIdent: LIdent = r"!\w+"  => LIdent::from_str(<>).unwrap();
RIdent: RIdent = r"~\w+"  => RIdent::from_str(<>).unwrap();

Number: f64 = r"[0-9]+(\.[0-9]+)?" => f64::from_str(<>).unwrap();

String: String = r#""([^"\\]|\\["\\])*""# => String::from(<>.get(1 .. <>.len()-1).unwrap());
